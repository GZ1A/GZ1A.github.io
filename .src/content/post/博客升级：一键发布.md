---
# 常用定义
draft: false	                

title: "【博客升级】用 bat 实现 Hugo 一键发布至 GitHub"
date: 2020-02-12T00:17:41+08:00					# 创建时间
author: "昼阴夜阳"             			# 作者

# 分类和标签
categories: ["技术"]		            # 分类
tags: ["博客升级","bat","Hugo"]  						# 标签，可设置多个，用逗号隔开。Hugo会自动生成标签的子URL

# 用户自定义
comment: true	 # 关闭评论
toc: true	 	 # 关闭文章目录
contentCopyright: '<a rel="license noopener" href="https://creativecommons.org/licenses/by-nc-nd/4.0/" target="_blank">CC BY-NC-ND 4.0</a>'	#自定义文章的版权规则
reward: true     # 关闭打赏
mathjax: true    # 打开 mathjax
---

hugo 还是挺方便的，但是在写完 markdown 文档到发布到 github 之间还有好多行命令要打，太累了。于是很自然的想到使用bat批处理脚本帮我输入这些命令，自动发布。

## 搜索资料

一开始找到的也是最相关的一篇博客是 [编写bat批处理脚本文件来设置自动推送](https://www.yixuju.cn/programming/batch/hugo-push/) ，需求完全一致。为了可能的修改，也找到了一个 [批处理入门手册](https://www.w3cschool.cn/pclrmsc/tdsgnn.html) 。

还发现了一个可以绑定仓库的 markdown 编辑器， [小书匠编辑器](https://www.dazhuanlan.com/2019/10/14/5da3e834e0c1d/) ，还有[快捷键](https://www.cnblogs.com/wushuaishuai/p/9308094.html#_label1_3) ，看起来整挺好。但是我也挺喜欢现在在用的 Typora 所以暂且观望。

## 配置 Config

发布分两步，生成和推送。生成命令很简单，只要一句 `hugo` 就可以重新生成当前工作区里的站点——根据站点路径下的 `config.toml` 这个文件。我正在使用的主题的[文档](https://github.com/xianmin/hugo-theme-jane/blob/master/README-zh.md)中有提到如何进行站点设置，配置站点的 config.toml ，根据注释进行相应的修改就可以了。

## 设置 bat

根据资料里的博客，对开头的配置变量进行了以下修改，得到以下的 bat 文件。

```shell
@echo off
set pan=.\public\
set repo=git@github.com:GZ1A/GZ1A.github.io.git
set branch=master
if exist %pan% (
    
    echo "clean public directory"
    rd /S /Q %pan%
    echo "Hugo again for new site"
    hugo
) else (
    echo "can not find public directory"
    echo "Hugo again for new site"
    hugo
)
if exist %pan% (
    cd %pan%
    echo "git init and commit"
    git init
    git add -A
    git commit -m "update site at %time%"
    echo "set remote repository and push"
    git remote add origin %repo%
    git push -f origin master:master -v
) else (
    echo "can not find public directory, hugo fail!"
)
pause
```

然后我发现这段命令中没有位置让我输入commit message，当场就开始魔改了。大概就加了这两句话

```shell
if exist %pan% (
    rem ...
    set /p commitMessage=Commit Message:
    if "%commitMessage%"=="" (
        set commitMessage=update site
    )
    echo cm:%commitMessage%
    rem ...
) 
```

然而 echo 输出的一直是空的，也就是说我没有输入成功！顿时开始怀疑我这10分钟学的shell。寻思着我哪里写的不对，就开始继续研究基础。研究了一个小时，突然发现了盲点：会不会是这个嵌套出了问题。于是百度。[批处理里set语句怎样在if语句中使用](https://zhidao.baidu.com/question/454906059021031085.html) 。只要在 if 外增加两句话，并把引用时的 `%` 改为 `!` 就可以了。

泪，流了下来。我悟了！以后自信点，~~遇到错误多找找别人的问题，不要老是想着是自己的问题~~  **不懂就问  ！！**（指百度)  还要先提纲挈领，哪怕不细看也得有个大概了解，至少知道可能出现的问题。

```shell
setlocal enabledelayedexpansion
if exist %pan% (
    rem ...
	set /p commitMessage=Commit Message:
    if "!commitMessage!"=="" (
        set commitMessage=update site
    )
    echo cm:!commitMessage!
    rem ...
)
endlocal
```

总之问题是解决了。

原来的代码还有别的问题，比如会强制清空这个仓库，搞得云端记录都无了。总之稍微修改了一下。

```shell
@echo off
set pan=.\public\
set repo=git@github.com:GZ1A/GZ1A.github.io.git
set branch=master
set default=1

if exist %pan% (
    
    echo "clean public directory"
    rd /S /Q %pan%
    echo "Hugo again for new site"
    hugo
) else (
    echo "can not find public directory"
    echo "Hugo again for new site"
    set default=1
    hugo
)

setlocal enabledelayedexpansion
if exist !pan! (
    cd !pan!
    echo "git commit"
    git add -A
    echo.
    set /p commitMessage=Commit Message:
    if "!commitMessage!"=="" (
        set commitMessage=update site
    )
    git commit -m "!commitMessage!"
    echo "set remote repository and push"
    git push origin master

    REM git init
    REM git remote add origin %repo%
    REM git push -f origin master
) else (
    echo "can not find public directory, hugo fail!"
    set default=0
)
endlocal

if %default% neq 1 (
    pause
)
pause
```

~~终于，单论发布这块已经拿下了。接下来可能还要对博客做点工作。~~

## UPDATE

### 20200213 修复404

本来今天高高兴兴。 没想到打开主页却是 404 。明明在本地用 `hugo server` 测试的时候没啥问题。

百度发现了一样的问题：[hexo 搭建个人博客，提交后github后显示404...](https://segmentfault.com/q/1010000007578473?_ea=1390393) 。感谢 Dont 的解答， [GitHub Pages](https://help.github.com/en/github/working-with-github-pages) 实现了在访问 xxx.github.io 时会访问对应用户仓库 master 分支，当没有指定名文件时，HTTP请就会被解释为访问该路径下的 index.html 。

文档里的命令只 push 了 public 文件夹，而我 copy 来的代码是把整个工程都 push 上去了（还覆盖了原来的文件）。按照分支的解决方法，试图优雅的分出工程和发布两个分支，但我的 git （还有 bat ）学艺不精，眼看要陷入 debug 地狱了。遂放弃。 选择把工程放到 .src 文件夹里，再把 public 里的内容全部 copy 到外面的方法。

经过一个晚上的学习和调试（花了半个晚上领悟 cmd 里 `/` 跟着参数，`\` 分割目录，这就是Windows标准吗）。最终得到了一键发布的 bat。

### 20200214 添加 CNAME

在添加域名后就又有了新的文件 CNAME ，为了防止重新生成时自动清除了 CNAME ，需要再进行一下判断。

### 20200217  添加远程仓库

添加了 Coding 的仓库，在发布的时候就要推送两次。顺便在推送前对远程仓库分别进行一次 Pull ，防止在多个电脑上写博客时可能出现的问题。

## 成果

[github 仓库里的 publish.bat](https://github.com/GZ1A/GZ1A.github.io/blob/master/.src/publish.bat)

```shell
@echo off
REM dont commit if DEBUG = 1
set debug=0

REM Const
set build=.\public
set dst=..\..\site
set repo=git@github.com:GZ1A/GZ1A.github.io.git

REM Var
set default=1

if exist %build% (
    echo "clean public directory"
    rd /S /Q %build%
    echo "Hugo again for new site"
    hugo
) else (
    echo "can not find public directory"
    echo "Hugo again for new site"
    set default=1
    hugo
)

REM delete old build
echo "delete old build"
pushd "%dst%"
for %%i in ("*.*") do (
    if not "%%i"==".gitignore" (
    if not "%%i"=="CNAME" (
        del /q "%%i"
    )
    )
)
for /d %%i in ("*.*") do (
    if not "%%i"==".src" (
    if not "%%i"==".git" (
        rd /q /s "%%i"
    )
    )
)
popd

xcopy "%build%" "%dst%" /s /q /f /h /y

setlocal enabledelayedexpansion
if exist !build! (
    cd !build!
    if !debug! neq 1 (

        REM Code for first time
        REM git init
        REM git remote add origin %repo%
        REM git push -f origin master

        echo "git pull"
        git pull origin master
        REM another remote repo
        git pull coding_repo master

        echo "git commit"
        git add -A
        echo.
        set /p commitMessage=Commit Message:
        if "!commitMessage!"=="" (
            set commitMessage=update site
        )
        git commit -m "!commitMessage!"

        echo "git push"
        git push origin master
        REM another remote repo
        git push coding_repo master
    )

) else (
    echo "can not find public directory, hugo fail!"
    set default=0
)
endlocal

if %default% neq 1 (
    pause
)
if %debug% neq 0 (
    pause
)
```

