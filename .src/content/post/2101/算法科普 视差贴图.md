---
# 常用定义
draft: false

title: "算法科普 Parallax Mapping"
date: 2021-01-26T20:17:55+08:00				# 创建时间
author: "昼阴夜阳"             				# 作者

# 分类和标签
categories: ["技术"]		            		# 分类
tags: ["RayMarching"]		    		# 标签

---

## 视差贴图

[Parallax Mapping](https://learnopengl.com/Advanced-Lighting/Parallax-Mapping)

视差贴图是一种类似于法线贴图的技术，通过 **欺骗用户** 高效的提高纹理表面细节。

<img src="https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/image-20210127041231980.png" alt="image-20210127041231980" style="zoom:50%;" />

如图的砖墙由 **两个三角形** 渲染而成。

法线贴图改变了像素的法线采样，而视差贴图改变了像素的纹理采样坐标。核心思想是 **改变纹理坐标** 。红线代表砖体，黑色代表面片。如果存在砖墙的模型，相机看到的应该是 B点 ，但现在只有面片，所以看到的是 A点 。为了模拟有砖墙的情况，我们需要让 A 点能像 B 点一样渲染。采样 B点 的 **纹理贴图** 信息，就可以知道 漫反射、法线 等等信息。

![image-20210127040743113](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/parallax_mapping_plane_height.png)

所以采样 B点 的法线和贴图，最重要的一步是要获得 B点 的 **纹理坐标**。

### 计算纹理坐标

很明显，B = A + viewDir * `深度`，但是得到的 B 还需要转换成纹理坐标。为了避免麻烦的转换，不如直接在 **切线空间**  中进行计算。这样算出来的 B 坐标的 x,y 就和贴图对齐了！

然后需要计算 `深度`。因为要节省算力（懒），解析是不可能解析的，甚至连近似的采样都不想做。就先用规律近似一下。

> 高度的变化通常比较平滑，视角又通常接近垂直——就直接把高度当做这 `深度` 好了

片元着色器的参考代码如下

```glsl
// offset texture coordinates with Parallax Mapping
vec3 viewDir   = normalize(fs_in.TangentViewPos - fs_in.TangentFragPos);
vec2 texCoords = ParallaxMapping(fs_in.TexCoords,  viewDir);
if(texCoords.x > 1.0 || texCoords.y > 1.0 || texCoords.x < 0.0 || texCoords.y < 0.0)
    discard;	// 防止采样超出贴图范围

// then sample textures with new texture coords
vec3 diffuse = texture(diffuseMap, texCoords);
vec3 normal  = texture(normalMap, texCoords);
normal = normalize(normal * 2.0 - 1.0);
// proceed with lighting code
[...] 

// 直接把高度当做深度
vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
{ 
    float height =  texture(depthMap, texCoords).r;    
    vec2 p = viewDir.xy / viewDir.z * (height * height_scale);
    return texCoords - p;    // 注意方向
} 
```

其中包含  `/ viewDir.z ` 这个除法的技术被称为 `带偏移限制的视差映射`。这个项是在视角大时使得偏移变大，垂直时不改变的简单补正。

![image-20210127055300675](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/image-20210127055300675.png)

### 更多采样

如上的近似在垂直效果挺好，但在侧面看还会有一些问题。所以就通过更多的采样提高深度计算的精度。

* 因为画深度更方便，所以用深度图而不是高度图了。

![](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/parallax_mapping_steep_parallax_mapping_diagram.png)

将总深度范围划分为相同高度/深度的多层，从上往下遍历深度层，从T0开始移动纹理坐标（步进到下一层），直到深度层对应的深度大于深度图（模型）的深度，说明采样点（纹理坐标T3）已经超过了碰撞点。要求的纹理坐标应该在这次和上次（T3/T2）之间。返回这次的坐标，就可以得到更精确的结果。

```glsl
vec2 ParallaxMapping(vec2 texCoords, vec3 viewDir)
{ 
    /// 确定一些变量和参数
    // number of depth layers
    const float numLayers = 10;
    // calculate the size of each layer
    float layerDepth = 1.0 / numLayers;
    // depth of current layer
    float currentLayerDepth = 0.0;
    // the amount to shift the texture coordinates per layer (from vector P)
    vec2 P = viewDir.xy * height_scale; 
    vec2 deltaTexCoords = P / numLayers;
  
	/// 然后开始遍历
    // get initial values
    vec2  currentTexCoords     = texCoords;
    float currentDepthMapValue = texture(depthMap, currentTexCoords).r;

    while(currentLayerDepth < currentDepthMapValue)
    {
        // shift texture coordinates along direction of P
        currentTexCoords -= deltaTexCoords;
        // get depthmap value at current texture coordinates
        currentDepthMapValue = texture(depthMap, currentTexCoords).r;  
        // get depth of next layer
        currentLayerDepth += layerDepth;  
    }

    return currentTexCoords;
}   
```

### 优化

针对特性做一些优化。

#### 智能采样（朴素版

视角大时出问题多，所以就增加采样吧。而视角小的时候少采样也确实无所谓。

```glsl
const float minLayers = 8.0;
const float maxLayers = 32.0;
float numLayers = mix(maxLayers, minLayers, max(dot(vec3(0.0, 0.0, 1.0), viewDir), 0.0));  
```

#### Parallax Occlusion Mapping 视差遮蔽贴图

前后两次采样中 **深度与深度层** 的差值（就是还差多少碰撞） 做线性插值，然后加权到这两次采样的深度层上。

![](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/parallax_mapping_parallax_occlusion_mapping_diagram.png)

```glsl
[...] // steep parallax mapping code here
  
// get texture coordinates before collision (reverse operations)
vec2 prevTexCoords = currentTexCoords + deltaTexCoords;

// get depth after and before collision for linear interpolation
float afterDepth  = currentDepthMapValue - currentLayerDepth;
float beforeDepth = texture(depthMap, prevTexCoords).r - currentLayerDepth + layerDepth;
 
// interpolation of texture coordinates
float weight = afterDepth / (afterDepth - beforeDepth);
vec2 finalTexCoords = prevTexCoords * weight + currentTexCoords * (1.0 - weight);

return finalTexCoords;  
```

![image-20210127064733423](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/image-20210127064733423.png)