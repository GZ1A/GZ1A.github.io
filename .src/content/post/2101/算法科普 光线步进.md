---
# 常用定义
draft: false

title: "算法科普 RayMarching"
date: 2021-01-25T21:13:35+08:00				# 创建时间
author: "昼阴夜阳"             				# 作者

# 分类和标签
categories: ["技术"]		            		# 分类
tags: ["RayMarching"]		    		# 标签

---

## 参考

[Ray Marching for Dummies!](https://www.youtube.com/watch?v=PGtv-dBi2wE&t&ab_channel=TheArtofCode)

[Shader Toy](https://www.shadertoy.com/)

[3D SDF](https://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm)

## Ray Marching 初见

Ray Marching / Sphere Tracing

Marching along the Ray

![image-20210125214716698](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/02/image-20210125214716698.png)

![image-20210125211720208](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/02/image-20210125211720208.png)

* 从相机参数出发，得到 **ViewRay**
* 射线检测，得到 **距离**
* 根据距离材质和灯光，得到 **像素颜色**

和Ray Tracing 区别：

在射线检测环节，Ray Marching 是步进式采样得到的数值解，而 Ray Tracing 是通过方程得到的精确解析解。

### Camera 处理

* ro | ray origin，相机位置 
* rd | ray direction

```glsl
void mainImage(out vec4 fragColor,in vec2 fragCoord)
{
    vec2 uv = (fragCoord - .5*iResolution.xy)/iResolution.y;
    
    vec3 col = vec3(0.0);
    
    vec3 ro = vec3(0,1,0);
    vec3 rd = normalize(vec3(uv.x,uv.y,1))    
   
    fragColor = vec4(col,1.0);
}
```

### Ray Intersection

![image-20210125214939819_2](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/02/image-20210125214939819_2.png)

循环检测距离，直到足够近判断为碰撞或者没碰到物体，足够远。

### Light

获得射线和场景交点之后就是进行光照计算了。定义光源后，计算得到 lightDir 和 normal，然后用 Lambert 模型。

```glsl
vec3 p = ro+rd*dist;
float dif = GetLight(p);
col += vec3(dif);

// ...
float GetLight(vec3 p)
{
    vec3 lightPos = vec3(0,5,2);
    lightPos.xz += 5. * vec2(sin(iTime), cos(iTime));
    
    vec3 l = normalize(lightPos-p);    
    vec3 n = GetNormal(p);
    
    float dif = clamp( dot(n,l),0.,1.);
    
    return dif;
}
```

#### Normal

因为是数值拟合，法线也是拟合得到的。通过在拟合点临近采样，微分。

`Normal (x) ≈ deltaDist / deltaX`

从而获得法线分量。

```glsl
vec3 GetNormal(vec3 p)
{
    float d = GetDist(p);
    vec2 e = vec2(.01,0);
    
    vec3 n = d-vec3(
        GetDist(p-e.xyy),
        GetDist(p-e.yxy),
        GetDist(p-e.yyx));
        
    return normalize(n);
}
```

#### Shadow

类似光追，命中平面后尝试从交点向光源进行 Ray Marching ，**寻找和场景间的交点**，如果距离小于两点距离，说明光源被遮挡，当前位置处于阴影中。

```glsl
// ...
vec3 eLight = n * SURF_DIST * 2.;
float distLight = RayMarching(p + eLight, l);
float dif = clamp(dot(n, l), 0., 1.);
if (distLight < length(p - lightPos))
    dif *= .1;
```

### 初见

```glsl
#define MAX_STEPS 100
# define MAX_DIST 100.
# define SURF_DIST 0.01

float GetDist(vec3 p) {
    vec4 sphere = vec4(-0.9, 1, 3, 1);
    vec4 sphere2 = vec4(0.7, 1, 6, 1);

    float planeDist = p.y;
    float plane2Dist = 300.0 - p.z;
    float sphereDist = distance(p, sphere.xyz) - sphere.w;
    float sphere2Dist = distance(p, sphere2.xyz) - sphere2.w;

    float d = min(min(min(planeDist, sphereDist), sphere2Dist), plane2Dist);
    return d;
}

float RayMarching(vec3 ro, vec3 rd) {
    float dO = 0.;
    for (int i = 0; i < MAX_STEPS; i++) {
        // cal dist between p and objs then compare
        vec3 p = ro + dO * rd;
        float dS = GetDist(p);
        dO += dS;
        if (dS < SURF_DIST || dO > MAX_DIST) break;
    }
    return dO;
}

vec3 GetNormal(vec3 p) {
    float d = GetDist(p);
    vec2 e = vec2(.01, 0);

    vec3 n = d - vec3(
        GetDist(p - e.xyy),
        GetDist(p - e.yxy),
        GetDist(p - e.yyx));

    return normalize(n);

}

float GetLight(vec3 p) {
    vec3 lightPos = vec3(0, 5, 3);
    lightPos.xz += 5. * vec2(sin(iTime), cos(iTime));

    vec3 l = normalize(lightPos - p);
    vec3 n = GetNormal(p);

    // check shadow
    vec3 eLight = n * SURF_DIST * 2.;
    float distLight = RayMarching(p + eLight, l);
    float dif = clamp(dot(n, l), 0., 1.);
    if (distLight < length(p - lightPos))
        dif *= .1;

    return dif;
}

void mainImage(out vec4 fragColor, in vec2 fragCoord) {
    vec2 uv = (fragCoord - .5 * iResolution.xy) / iResolution.y;

    vec3 col = vec3(0.0);

    vec3 ro = vec3(0, 2, -2);
    vec3 rd = normalize(vec3(uv.x, uv.y, 1));

    float dist = RayMarching(ro, rd);

    vec3 p = ro + rd * dist;
    float dif = GetLight(p);

    col += vec3(dif);

    col += vec3(.2);
    col *= vec3(.3, .5, .8);

    col.r = .5 - col.r;

    // = depth =
    //dist /= 20.;
    //col += vec3(dist);

    fragColor = vec4(col, 1.0);
}
```

## 3D SDF

[3D SDF 参考](https://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm)

[Ray Marching Distance Fields](https://www.iquilezles.org/www/articles/raymarchingdf/raymarchingdf.htm)

Raymarching SDFs (Signed Distance Fields, or Functions sometimes) （有符号距离场 / 函数）优雅而简单，且是一种渲染3D模型的强大方法。

各种几何体的距离函数，用于 RayMarching 参考。

## 正态分布

概率论分布中，**正态分布**(高斯分布) 是实值随机变量的连续概率分布。数学期望为μ、方差为σ^2的正态分布的概率密度函数为

![image-20210226010652188](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/02/image-20210226010652188.png)

变量 X 服从正态分布，记作

![image-20210226010706874](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/02/image-20210226010706874.png)

![image-20210127040816606](https://gitee.com/GZ1A/image-hosting/raw/master/blog/2021/01/image-20210127040816606.png)

* 横轴区间（μ-σ,μ+σ）内的面积为68.268949%
* 横轴区间（μ-2σ,μ+2σ）内的面积为95.449974%。
* 横轴区间（μ-3σ,μ+3σ）内的面积为99.730020%。

